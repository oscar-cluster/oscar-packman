#!/usr/bin/env perl 
#
# Copyright (c) 2007-2008 Oak Ridge National Laboratory
#                         Geoffroy Vallee <valleegr@ornl.gov>
#                         All rights reserved
#
# This file is part of the OSCAR software package.  For license
# information, see the COPYING file in the top level directory of the
# OSCAR source distribution.
#
# $Id$
#

BEGIN {
    if (defined $ENV{OSCAR_HOME}) {
        unshift @INC, "$ENV{OSCAR_HOME}/lib";
    }
}

use strict;
use warnings "all";
use Carp;
use OSCAR::PackageSmart;
use OSCAR::PackManDefs;
use OSCAR::Env;
use OSCAR::Logger;
use OSCAR::LoggerDefs;
use Getopt::Long;
use Data::Dumper;

my ($prepare_repo, $root, $distro);
my ($verbose,$debug);

Getopt::Long::Configure("pass_through");
GetOptions(
        "distro=s"                      => \$distro,
        "prepare-repo=s"                => \$prepare_repo,
        "root=s"                        => \$root,
        "verbose"                       => \$verbose,
        "v"                             => \$verbose,
        "debug"                         => \$debug,
        "d"                             => \$debug,
        "help"                          => \&help_and_exit,
        ) || help_and_die();

if($verbose) {
    $OSCAR::Env::oscar_verbose = 5;
    $ENV{PACKMAN_VERBOSE} = 5;
} elsif($debug) {
    $OSCAR::Env::oscar_verbose = 10;
    $ENV{PACKMAN_VERBOSE} = 10;
}

# Validate the packman command requested by the user.
#
# Input: cmd, the command that the user wants to execute.
# Return: 1 if the command is valid, 0 else.
sub command_is_valid ($) {
    my $cmd = shift;

    if ( OSCAR::Utils::is_a_valid_string ($cmd) && (($cmd eq "install")
        || ($cmd eq "remove") || ($cmd eq "search") || ($cmd eq "show") 
        || ($cmd eq "list"))) {
        return 1;
    } else {
        return 0;
    }
}

if ($prepare_repo && defined $ARGV[0]) {
    oscar_log(1, ERROR, "It seems you try to do multiple actions at the same time, ".
        "this is not allowed.");
    exit 1;
}

if ($prepare_repo) {
    oscar_log(5, INFO, "Preparing repository $prepare_repo");
    my $pm = OSCAR::PackageSmart::prepare_pool ($prepare_repo);
    if (!defined $pm) {
        oscar_log(1, ERROR, "Impossible to prepare the repository $prepare_repo");
    } else {
        oscar_log(1, INFO, "Repository $prepare_repo is now ready.");
    }
    exit 0;
}

my $cmd = $ARGV[0];
if ( command_is_valid ($cmd) == 0 ) {
    oscar_log(1, ERROR, "You try to execute an unsupported command $ARGV[0]");
    exit 1;
} else {
    if (!OSCAR::Utils::is_a_valid_string ($distro)) {
        # if the distro is not specified, we try to detect the local distro
        $distro = OSCAR::PackagePath::get_distro ();
        if (!OSCAR::Utils::is_a_valid_string ($distro)) {
            oscar_log(1, ERROR, "Distro not specified and detection impossible");
            exit 1;
        }
    }

    # Based on the distro id we detect the underneath binary package format
    # (remember that PackMan cannot abstract that format).
    require OSCAR::PackagePath;
    my ($dist, $ver, $arch) 
        = OSCAR::PackagePath::decompose_distro_id ($distro);
    my $os = OSCAR::OCA::OS_Detect::open (fake=>{ distro=>$dist,
                                                  distro_version=>$ver,
                                                  arch=>$arch});
    (oscar_log(1, ERROR, "Impossible to decompose the distro ID"), exit 1) if (!defined $os);
    oscar_log(5, INFO, "Binary package format: ". $os->{pkg});
    require OSCAR::PackMan;
    my $pm;
    if ($os->{pkg} eq "deb") {
        $pm = OSCAR::PackMan::DEB->new;
    } elsif ($os->{pkg} eq "rpm") {
        $pm = OSCAR::PackMan::RPM->new;
    } else {
        oscar_log(1, ERROR, "Unknown binary package format (".$os->{pkg});
        exit 1;
    }
    
    if (!defined $pm) {
        oscar_log(1, ERROR, "Unable to create a PackMan object");
        exit 1;
    }
    if ($pm->distro($distro) == 0) {
        oscar_log(1, ERROR, "Impossible to specify the distro of the PackMan object");
        exit 1;
    }
    oscar_log(5, INFO, "PM Status: $pm->status");
    if (defined $root) {
        if ($pm->chroot($root) == 0) {
            oscar_log(1, ERROR, "Impossible to specify the root of the PackMan object");
            exit 1;
        }
    }

    #
    # For most of the commands, the argument is a list of packages. So, we
    # parse the arguments/
    #
    my @pkgs;
    my $pattern;
    if ($cmd ne "search") {
        my $pos = 1;
        foreach my $p (1 .. $#ARGV ) {
            push (@pkgs, $ARGV[$p]);
            $pos++;
        }
        if ($cmd ne "list" && scalar (@pkgs) == 0) {
            oscar_log(5, INFO, "No packages to look for.");
            exit 0;
        }
    } else {
        $pattern = $ARGV[1]
    }
    my $msg = "Executing commands based on the distro $distro";
    $msg .= " (in $root)" if (($OSCAR::Env::oscar_verbose >= 5) && defined $root);
    oscar_log(5, INFO, "$msg");
    my ($rc, @output);
    if ($cmd eq "install") {
        oscar_log(5, INFO, "Packages to install: " . join (", ", @pkgs));
        ($rc, @output) = $pm->smart_install(@pkgs);
        if ($rc == PM_ERROR) {
            oscar_log(1, ERROR, "Failed to install " . join (", ", @pkgs) . " ("
                . join ("\n", @output) . ")");
        }
    } elsif ($cmd eq "remove") {
        ($rc, @output) = $pm->smart_remove(@pkgs);
        if ($rc == PM_ERROR) {
            oscar_log(1, ERROR, "Failed to remove " . join (", ", @pkgs));
        }
    } elsif ($cmd eq "search") {
        ($rc, @output) = $pm->search_repo($pattern);
        if ($rc != 0) {
            oscar_log(1, ERROR, "Unable to search for packages ($rc, $pattern, \n" .
                join ("\n", @output) . ")");
        } else {
            oscar_log(1, NONE, join ("\n", @output));
        }
    } elsif ($cmd eq "show") {
        my %data;
        ($rc, %data) = $pm->show_repo(@pkgs);
        if ($rc != 0) {
            oscar_log(1, ERROR, "Unable to show details of packages "
                . join (", ", @pkgs));
        } else {
            print Dumper %data;
        }
    } elsif ($cmd eq "list") {
        my @data;
        oscar_log(1, INFO, "Querying all installed packages...");
        @data = $pm->query_list_installed_pkgs();
        OSCAR::Utils::print_array (@data);
    } else {
        oscar_log(1, ERROR, "Unsupported command ($cmd)");
    }
    exit 0;
}

sub help () {
    print "Please execute \"man packman\"\n";
}

sub help_and_die {
    help ();
    exit 1;
}

sub help_and_exit {
    help ();
    exit 0;
}

help_and_die();

exit 1;


__END__

=head1 NAME

packman, a command line tool for the PackMan Perl module.

=head1 DESCRIPTION

The packman script is a simple command line interface (CLI) for the PackMan
Perl module. Therefore, this script allows one to manipulate binary packages
transparently, the underneath binary package format being abstracted.

=head1 SYNTAX

packman [OPTIONS] COMMANDS

=head1 OPTIONS

Recognized options include:

=over 8

=item --distro

Specify the Linux distribution that should be used to perform the commands. 
Typically this parameter will allow PackMan to identify the OSCAR repositories
that must be used for the command execution.

=item --prepare-repo <repo_path>

Prepare a local repository of binary packages (repository available at "repo_path").

=item --root

Specify the root where the commands should be executed (a chroot is typically used).

=item --verbose | --v

Enable the verbose mode (usefull for debugging).

=item --help

Help information.

=back

=head1 Examples

=over 8

=item Package installation:

Install the dpkg package into the image stored in /tmp/test:

sudo packman --root /tmp/test/ --distro ubuntu-7-i386 install dpkg 

Note that if the image does not already exists, the image will be bootstrapped.

=back

=head1 AUTHOR

Geoffroy Vallee, Oak Ridge National Laboratory <valleegr at ornl dot gov>

=head1 SEE ALSO

perl(1), perldoc OSCAR::PackMan, perldoc OSCAR::PackMan::DEB, perldoc OSCAR::PackMan::RPM

=cut
